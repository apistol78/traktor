<?xml version="1.0" encoding="utf-8"?>
<object type="traktor.render.ShaderModule" version="2">
	<text><![CDATA[
	
// https://learnopengl.com/PBR/Lighting

/*! Calculate fresnel term.
 * How much the surface reflects light versus how much it refracts light.
 */
vec3 FresnelSchlick(float cosTheta, vec3 F0)
{
	cosTheta = max(cosTheta, 0.0f);
	return F0 + (1.0f - F0) * pow(clamp(1.0f - cosTheta, 0.0f, 1.0f), 5.0f);
}

vec3 FresnelSchlickRoughness(float cosTheta, vec3 F0, float roughness)
{
    return F0 + (max(vec3(1.0f - roughness), F0) - F0) * pow(clamp(1.0f - cosTheta, 0.0f, 1.0f), 5.0f);
}  

/*! Normal distribution function. */
float DistributionGGX(vec3 N, vec3 H, float roughness)
{
	const float PI = 3.14159265359f;
	const float a = roughness * roughness;
	const float a2 = a * a;
	const float NdotH = max(dot(N, H), 0.0f);
	const float NdotH2 = NdotH * NdotH;
	const float num = a2;
	float denom = (NdotH2 * (a2 - 1.0f) + 1.0f);
	denom = PI * denom * denom;
	return num / denom;
}

/*! Geometry function. */
float GeometrySchlickGGX(float NdotV, float roughness)
{
	const float r = (roughness + 1.0f);
	const float k = (r * r) / 8.0f;
	const float num = NdotV;
	const float denom = NdotV * (1.0f - k) + k;
	return num / denom;
}

/*! Geometry function. */
float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
	const float NdotV = max(dot(N, V), 0.0f);
	const float NdotL = max(dot(N, L), 0.0f);
	const float ggx2 = GeometrySchlickGGX(NdotV, roughness);
	const float ggx1 = GeometrySchlickGGX(NdotL, roughness);
	return ggx1 * ggx2;
}

/*! Calculate lighting. */
vec3 CalculateLighting(vec3 N, vec3 V, vec3 L, vec3 albedo, float metallic, float roughness, vec3 radiance, float specularG)
{
	const float PI = 3.14159265359f;
	const vec3 H = normalize(V + L);

	const float NdotL = max(dot(N, L), 0.0f);
	const float NdotV = max(dot(N, V), 0.0f);

	const vec3 F0 = mix(vec3(0.01f), albedo, metallic);
	const vec3 F = FresnelSchlickRoughness(NdotV, F0, roughness);

	const float NDF = DistributionGGX(N, H, roughness);
	const float G = GeometrySmith(N, V, L, roughness);

	const vec3 numerator = NDF * G * F;
	const float denominator = 4.0f * NdotV * NdotL + 0.0001f;

	const vec3 specular = numerator / denominator; 
	const vec3 kS = F;
	const vec3 kD = (vec3(1.0f) - kS);

	return (kD * albedo / PI + specular * specularG) * radiance * NdotL;
}

/*! Calculate reflection. */
vec3 CalculateReflection(vec3 N, vec3 V, vec3 albedo, float metallic, float roughness)
{
	const float NdotV = max(dot(N, V), 0.1f);

	const vec3 F0 = mix(vec3(0.01f), albedo, metallic);
	const vec3 F = FresnelSchlickRoughness(NdotV, F0, roughness);

	return clamp(F, 0.0f, 1.0f);
}

	
	]]></text>
	<samplers/>
	<structDeclarations/>
</object>
